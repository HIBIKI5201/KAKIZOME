#pragma kernel Phase0
#pragma kernel Phase1

RWStructuredBuffer<float3> PositionBuffer;
RWStructuredBuffer<float3> TargetBuffer;

float DeltaTime;
uint ParticleCount;

float StopDistance;
float Speed;

RWStructuredBuffer<uint> Phase0Indices;
uint Phase0Count;
RWStructuredBuffer<uint> Phase1Indices;
uint Phase1Count;

[numthreads(64, 1, 1)]
void Phase0(uint id : SV_DispatchThreadID)
{
    if (id >= Phase0Count) { return; }
    
    // インデックスバッファからパーティクルのインデックスを取得。
    uint particleIndex = Phase0Indices[id];
    
    float3 target = TargetBuffer[particleIndex];
    float3 pos = PositionBuffer[particleIndex];
    
    float3 dir = normalize(target - pos);
         
    // ターゲットと逆方向に移動。
    pos -= dir * Speed * DeltaTime;
    PositionBuffer[particleIndex] = pos;
}

[numthreads(64, 1, 1)]
void Phase1(uint id : SV_DispatchThreadID)
{
    if (id >= Phase1Count) { return; }
    
    // インデックスバッファからパーティクルのインデックスを取得。
    uint particleIndex = Phase1Indices[id];
    
    float3 target = TargetBuffer[particleIndex];
    float3 pos = PositionBuffer[particleIndex];
    
    float3 diff = target - pos;
    float dist = length(diff);
    float3 dir = diff / dist;
    
    if (dist <= StopDistance)
    {
        // ストップしたらターゲット位置にスナップ。
        PositionBuffer[particleIndex] = target;
        return;
    }
    
    // ターゲットに向かって移動。
    pos += dir * Speed * DeltaTime;
    PositionBuffer[particleIndex] = pos;
}
